# vim: set ft=zsh:

##
# Trigger a panic.
#
# Panics are intended for 'unrecoverable' logic errors only. They are **not** a
# replacement for exceptions (which shu doesn't provide). That said, panics
# *can* be ignored by setting `SHU_PANIC_IGNORE` to a non-zero value.
#
# Panics also produce a trace-back if `SHU_PANIC_TRACE` is set to a non-zero
# value. If >1, the trace-back includes file-absolute line numbers.
#
# This function uses `SHU_FD_STDERR` to bypass any redirection of `stderr` which
# might have taken place since the script was started.
#
# Panics send `SIGHUP` to the parent shell instance to ensure that it exits.
#
# @module core
#
# @param $1 ...
#   (optional) Zero or more strings to use as an error message. Multiple strings
#   are concatenated by spaces prior to being passed to `printf` (panic error
#   messages do not respect verbosity settings). Empty arguments are **not**
#   elided.
#
# @return SHU_EX_PANIC

local    i identifier line_no caller trace_line
local -a trace

# Build trace-back
(( $+functrace && $#functrace )) && {
  for i in {1..$#functrace}; do
    identifier=${functrace[i]%:*}
       line_no=${functrace[i]##*:}

    # Colourise and format depending on whether the identifier represents a
    # file, a special context like `(eval)`, or a regular function
    if
      { [[ $identifier == */* ]] && (( ! $+functions[$identifier] )) } ||
      { (( i == $#functrace ))   && [[ $identifier == $SHU_ARGZERO ]] }
    then
      (( i == 1 )) && caller="{${identifier:t}}"
      identifier="${SHU_ANSI[fg:yellow2]}{${identifier}}${SHU_ANSI[fg:reset]}"

    elif [[ $identifier == '('*')' ]]; then
      (( i == 1 )) && caller=$identifier
      identifier="${SHU_ANSI[fg:yellow2]}${identifier}${SHU_ANSI[fg:reset]}"

    else
      (( i == 1 )) && caller="${identifier}()"
      identifier="${SHU_ANSI[fg:yellow2]}${identifier}()${SHU_ANSI[fg:reset]}"
    fi

     trace_line=$identifier
    trace_line+=" @ ${SHU_ANSI[fg:magenta2]}${line_no}${SHU_ANSI[fg:reset]}"

    # Only add file-level info if requested
    (( SHU_PANIC_TRACE > 1 )) && {
      identifier=${${funcfiletrace[i]%:*}:-$ZSH_NAME}
         line_no=${funcfiletrace[i]##*:}
      trace_line+=' '
      trace_line+='('
      trace_line+="${SHU_ANSI[fg:yellow2]}${identifier:t}${SHU_ANSI[fg:reset]}"
      trace_line+=' @ '
      trace_line+="${SHU_ANSI[fg:magenta2]}${line_no}${SHU_ANSI[fg:reset]}"
      trace_line+=')'
    }

    trace+=( $trace_line )
  done
}

[[ $1 == -- ]] && shift

printf >&$SHU_FD_STDERR \
  '%s: %sPANIC in %s!%s%s\n' \
  $SHU_SCRIPT_NAME \
  "${SHU_ANSI[bg:red]}${SHU_ANSI[fg:white0]}" \
  "${caller:-(unknown)}" \
  "${SHU_ANSI[fg:reset]}${SHU_ANSI[bg:reset]}" \
  "${@:+ ${SHU_ANSI[fg:red]}${(j< >)@}}${SHU_ANSI[fg:reset]}" \

(( SHU_PANIC_TRACE )) &&
(( $#trace )) && {
  printf >&$SHU_FD_STDERR \
    '%s: Trace-back (most recent call on top):\n' \
    $SHU_SCRIPT_NAME

  for i in {1..$#trace}; do
    printf >&$SHU_FD_STDERR \
      '%3s. %s\n' $(( $#trace + 1 - i )) $trace[i]
  done
}

if [[ -o interactive ]] || (( SHU_PANIC_IGNORE )); then
  # Set an exit trap to force the caller to immediately return, unless we're at
  # the top level of a script or an interactive shell. As always, note that
  # exit traps can't run inside other exit traps
  (( ZSH_SUBSHELL || $#funcstack > 1 )) &&
  trap 'return SHU_EX_PANIC' EXIT
  return SHU_EX_PANIC
fi

# die_you_gravy_sucking_pig_dog
{
  # This will fail to properly kill child processes in some cases. For example,
  # when the panic occurs inside a sub-shell, the parent shell's background
  # children might not be cleaned up. `kill 0` would take care of whatever the
  # current process group is, but this can kill parent processes when `monitor`
  # isn't enabled. And we don't enable `monitor` because it prints those
  # irritating status messages...
  kill -HUP -- ${(@)${(@v)jobstates%=*}##*:}
  kill -HUP -- -$$ $$
  kill -HUP -- -$sysparams[pid]
} 2> /dev/null
exit SHU_EX_PANIC

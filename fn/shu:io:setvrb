# vim: set ft=zsh:

##
# Update output verbosity level.
#
# @module io
#
# @see shu:io:puts
#
# @param -n
#   (optional) Reset verbosity to level N0 (normal). Always behaves as though
#   `-x` were specified. This is the default if no other options are specified.
#
# @param -q
#   (optional) Reset verbosity to level Q1 (quiet) or, if already <= Q1 and `-x`
#   is not specified, reduce verbosity further.
#
# @param -q
#   (optional) Reset verbosity to level V1 (verbose) or, if already >= V1 and
#   `-x` is not specified, increase verbosity further.
#
# @param -x
#   (optional) Set verbosity to the exact level specified, regardless of the
#   current verbosity. For example, `-vvvx` sets the verbosity to exactly `V3`.
#
# @param -X
#   (optional) Decrease or increase verbosity level based on the current
#   verbosity (negate the effect of `-x`). This the default.
#
# @return 0

local OPTARG OPTIND opt last_opt verbosity=0 exact

while getopts :nqvxX opt; do
  case $opt in
    n) verbosity=0 ;;
    q)
      [[ $last_opt == $opt ]] || verbosity=0
      (( verbosity-- ))
      ;;
    v)
      [[ $last_opt == $opt ]] || verbosity=0
      (( verbosity++ ))
      ;;
    x) exact=1 ;;
    X) exact=0 ;;
    # Errors
    ':') shu::panic "Missing argument to option: ${(l:2::-:)OPTARG}" ;;
    '?') opt=$OPTARG ;&
    *)   shu::panic "Invalid option: ${(l:2::-:)opt}" ;;
  esac
  last_opt=$opt
done

shift $(( OPTIND - 1 ))

(( $# )) && shu::panic 'Too many operands'

if (( exact )); then
  SHU_VERBOSITY=$verbosity
else
  if (( verbosity == 0 )); then
    SHU_VERBOSITY=0
  elif
    (( verbosity <= SHU_VERBOSITY_Q1 )) &&
    (( SHU_VERBOSITY > SHU_VERBOSITY_Q1 ))
  then
    SHU_VERBOSITY=0
  elif
    (( verbosity >= SHU_VERBOSITY_V1 )) &&
    (( SHU_VERBOSITY < SHU_VERBOSITY_V1 ))
  then
    SHU_VERBOSITY=0
  fi
  (( SHU_VERBOSITY += verbosity ))
fi

if (( SHU_VERBOSITY < SHU_VERBOSITY_Q5 )); then
  SHU_VERBOSITY=$SHU_VERBOSITY_Q5
elif (( SHU_VERBOSITY > SHU_VERBOSITY_V5 )); then
  SHU_VERBOSITY=$SHU_VERBOSITY_V5
fi

return 0

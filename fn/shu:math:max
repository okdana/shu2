# vim: set ft=zsh:

# @todo WARNING: This function is untested

##
# Give the highest of a series of numbers.
#
# @module math
#
# @param $1 ... One or more numbers.
#
# @return 0

local max nctx

# Normal context?
[[ $0 == shu:math:* ]] && nctx=1

[[ ${1:-} == -- ]] && shift

(( $# )) || shu::panic 'Too few operands'

while (( $# )); do
  (( nctx )) &&
  [[ $1 != $~SHU_PAT_MATH_NUM ]] &&
  shu::panic "Invalid arithmetic argument: $1"

  : ${max:=$1}

  (( $1 > max )) && max=$1
  shift
done

# Normal context
(( nctx )) && {
  printf -r -- $max
  return 0
}
# Arithmetic context
(( max ))

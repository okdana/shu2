#!/usr/bin/env zsh

##
# shu-unit tests for shu:str:vercmp()

fn=${0:t:r}

# expected|version|constraint1|constraint2|...
provider=(
  # Basic numeric equality/inequality
  '0|1|1'
  '0|1.2|1.2'
  '0|1.2.3|1.2.3'
  '0|1.2.3|=1.2.3'
  '0|1.2.3|==1.2.3'
  '0|1|!=2'
  '0|1.2|!=1.3'
  '1|1.2.3|!=1.2.3'
  '1|1.2.3|==1.2.4'
  '0|1.2.3|!=1.2.4'
  '0|a|a'
  '0|a.b|a.b'
  '0|a.b.c|a.b.c'
  '1|a.b.c|!=a.b.c'
  '1|a.b.c|==a.b.d'
  '0|a.b.c|!=a.b.d'
  # Basic greater-than/less-than comparison
  '0|2|>1'
  '0|2|<3'
  '0|2|>=2'
  '0|2|<=2'
  '0|1.2|>1.1'
  '0|1.2|<1.3'
  '0|1.2|>=1.2'
  '0|1.2|<=1.3'
  '0|1.2.3|>1.2.2'
  '0|1.2.3|<1.2.4'
  '0|1.2.3|>=1.2.3'
  '0|1.2.3|<=1.2.3'
  '0|1.2.3|>1.2'
  '0|1.2.3|<1.3'
  '0|1.2.111|>1.2.12'
  '0|a.b.d|>a.b.c'
  '0|a.b.cc|<a.b.cd'
  '0|a.b.ccc|>a.b.cd'
  '1|1.2.3|<1.2.3'
  '1|a.b.c|<a.b.b'
  # Basic tilde-range comparison
  '0|1|~1'
  '1|2|~1'
  '0|1.1|~1.1'
  '0|1.5|~1.1'
  '1|2.0|~1.1'
  '0|1.1.1|~1.1.1'
  '0|1.1.5|~1.1.1'
  '1|1.2.0|~1.1.1'
  '1|2.0.0|~1.1.1'
  '0|1.1.1.1|~1.1.1.1'
  '0|1.1.1.5|~1.1.1.1'
  '1|1.1.2.0|~1.1.1.1'
  '1|1.2.0.0|~1.1.1.1'
  '1|2.0.0.0|~1.1.1.1'
  # Basic caret-range comparison
  '0|1|^1'
  '1|2|^1'
  '0|1.1|^1.1'
  '0|1.5|^1.1'
  '1|2.0|^1.1'
  '0|1.1.1|^1.1.1'
  '0|1.1.5|^1.1.1'
  '0|1.2.0|^1.1.1'
  '1|2.0.0|^1.1.1'
  '0|1.1.1.1|^1.1.1.1'
  '0|1.1.1.5|^1.1.1.1'
  '0|1.1.2.0|^1.1.1.1'
  '0|1.2.0.0|^1.1.1.1'
  '1|2.0.0.0|^1.1.1.1'
  # Basic wild-card comparison
  '0|1|*'
  '0|1.1|1.*'
  '0|1.1.1|1.*'
  '0|1.1.1|1.*.*'
  '0|1.1.1|~1.*'
  '0|1.1.1|^1.*'
  '1|1.1.1|>1.*'
  # Basic normalisation
  '0|1|0001'
  '0|1.0002|1.2'
  '0|1.2-3_4|1 2 3 4'
  '0|- - - 1.2 +|1.2'
  '0|1.2.a3|1.2.0a3'
  # Alpha/beta normalisation
  '0|alpha|a'
  '0|beta|b'
  '0|1-alpha|1-a'
  '0|1-beta|1-b'
  '0|1-alpha2|1-a-2'
  '0|1-beta2|1-b-2'
  '0|1-alpha2.3|1-a-2.3'
  '0|beta|>alpha'
  '0|alpha|<beta'
  # Multiple constraints
  '0|1.2.3|>=1.2|<=1.2.9|!=1.2.5'
  # Alternative operators
  '0|1.2.3|=>1.2|=<1.2.9|<>1.2.5'
  # Errors
  '199||'
  '199||1'
  '199|1|1|'
  '199|>=1|>=1'
  '199|1|='
  '199|1|>=.'
  '199|1|.'
  '199|1|#'
)

for p in $provider; do
  pa=( "${(@s<|>)p}" )

  @assert -d "Behaves as expected given arguments ${(j< >)${(@qq)pa[2,-1]}}" \
    -r $pa[1] -eq \
    $fn "${pa[2]}" "${(@)pa[3,-1]}"
done

@assert -d 'Panics with no arguments' \
  -r SHU_EX_PANIC -eq \
  $fn
@assert -d 'Panics with too few operands' \
  -r SHU_EX_PANIC -eq \
  $fn 1.2.3

for p in -Y +Y +v; do
  @assert -d "Panics with illegal option $p" \
    -r SHU_EX_PANIC -eq \
    $fn $p
done
